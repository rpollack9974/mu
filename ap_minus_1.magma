function generalized_Bernoulli(n,chi)
	f := Conductor(chi);
	ans := 0;
	Bn := BernoulliPolynomial(n);
	for a in [1..f] do
		ans := ans + chi(a)*Evaluate(Bn,a/f);
	end for;	

	return f^(n-1) * ans;
end function;

//Looks at T_p acting on S_k(SL_2(Z)) for (p,k) an irregular pair, and checks to see if there is any 
//eigenvalue defined over Z_p which is congruent to 1 mod p^2
function val_of_ap_minus_1(chi,minp,maxp)
	N := Conductor(chi);
	filename := "val_ap-1.N=" cat IntegerToString(N) cat ".txt";
	ps := [];
	for q in [minp..maxp] do
		if IsPrime(q) and (N mod q ne 0) then
			ps := ps cat [q];
		end if;
	end for;
	for j in [1..#ps] do
		p := ps[j];
		for k in [2..p-1] do
			if (-1)^k eq chi(-1) and Valuation(generalized_Bernoulli(k,chi),p) gt 0 then
				print "working with ",[p,k];
				PrintFile(filename,"Working with irregular pair (" cat IntegerToString(p) cat "," cat IntegerToString(k) cat ")");
				M := CuspidalSubspace(ModularSymbols(chi,k,1));
				S<y> := PolynomialRing(pAdicField(p,20));
				f := S!(HeckePolynomial(M,p));
				rs := Roots(f);
				for i in [1..#rs] do
					s := rs[i][1];
					e := Valuation(s-1);
					if e gt 0 then
						print "There is a root congruent to 1 modulo p ^",e;
						PrintFile(filename,"  There is a root congruent to 1 modulo p^" cat IntegerToString(e));
					end if;
				end for;
			end if;
		end for;
	end for;

	return "Done";
end function;


